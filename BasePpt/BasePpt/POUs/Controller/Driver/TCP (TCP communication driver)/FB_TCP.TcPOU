<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.36">
  <POU Name="FB_TCP" Id="{609a1fce-4485-45bf-bed3-527e862ab814}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TCP EXTENDS FB_CtrlBase
VAR
	/// Specific interface struct for public access
	SIf: T_Ctrl_SIf_TCP;
	intSendState: INT := 0;
	fbClientConnection: TC2_TCPIP.FB_ClientServerConnection;
	fbSend: TC2_TCPIP.FB_SocketSend;
	fbReceive: TC2_TCPIP.FB_SocketReceive;
	/// buffer for received messages
	fbReceiveBuffer: FB_DataSetFifo;
	/// buffer for messages to be sent
	fbSendBuffer: FB_DataSetFifo;
	stNextHead: ST_Header;
	bReceivedAck: BOOL;
	bReceivedNack: BOOL;
	iSendSize: UDINT;
	iNumSendTry: INT;
	tonSendTime: TON;
	pSendHeader: POINTER TO ST_Header;
	pByteSend: POINTER TO ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
	pReceiveHead: POINTER TO ST_Header;
	pByteReceive: POINTER TO ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
	naReceiveData: ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
	pReceiveData: POINTER TO ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
END_VAR
VAR CONSTANT
	cSendIdle: INT := 0;
	cSendBusy: INT := 1;
	cSendWait: INT := 2;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*__________________________________________________________________________________________________
¦	
¦	Vistaprint Schweiz GmbH
¦	CH - 8401 Winterthur
¦
¦	www.vistaprint.ch - info@vistaprint.ch
¦___________________________________________________________________________________________________

Function desription:



History:
Version		Date			Author		Comment
---------------------------------------------------------------------------------------------------
0.00.1		21.05.2014		AVME/DSC	Start history. Based on TCP client from jetter prototyp (Sonja Segmueller)
___________________________________________________________________________________________________*)


SUPER^();]]></ST>
    </Implementation>
    <Method Name="sta_CONNECT" Id="{012444c2-4f4c-4e85-8562-de1e9f46c149}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_CONNECT'}
METHOD sta_CONNECT : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;

		pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.SocketConnectionLost,0,ADR(SIf.Alm),E_AlmState.CLR,CIf.Job.eMe_ActivAlmClass);
	END_IF

// --------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		// check if connection has been lost
		IF (fbClientConnection.eState <> TC2_TCPIP.E_SocketConnectionState.eSOCKET_CONNECTED) THEN 
			pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.SocketConnectionLost,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);
			
			IF SIf.Cfg.bolAutoReconnectEnable THEN
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;
			ELSE	
				me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;
			END_IF
		END_IF
		
		CASE me.intCmd OF
			TCP_CmdState.cCMD_Close:
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;
			TCP_CmdState.cCMD_Connect:
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;
		END_CASE
	END_IF

// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Always" Id="{04443b31-aa81-49ea-8367-9dea755a08ac}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Always'}
METHOD PROTECTED Always : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// not inherite from base !!!  special behaviour for this TCP driver
// SUPER^.Always();

// Conditions Always
IF (me.bolChangeBackToAUTO=TRUE) THEN // switch mode to automatic switch off controller
	//me.StateMachine.intStateNext := cSTA_OFFBusy;
	me.bolChangeBackToAUTO := FALSE;
END_IF

IF (CIf.SwCfg.bolExist=FALSE) THEN // hardware of controller does not exist
	me.StateMachine.intStateNext := cSTA_SINGLE_ANSWER;
ELSE
	;
END_IF

// ----------------------------------------------------------------------------------
// cyclic actions outside of statemachine
IF (me.StateMachine.intState<>cSTA_SINGLE_ANSWER) THEN
	
	// Cyclic call of the client connection
	fbClientConnection();	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="StateMachine" Id="{3cb57e38-141e-4dd7-970e-0e4941e75bee}">
      <Declaration><![CDATA[{attribute 'object_name' := 'StateMachine'}
METHOD PROTECTED StateMachine : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.StateMachine();

// main statemachine of base controller
CASE me.StateMachine.intState OF
	TCP_CmdState.cSTA_Close:
		THIS^.sta_CLOSE();
    TCP_CmdState.cSTA_ConnectBusy:
		THIS^.sta_CONNECTBusy();
	TCP_CmdState.cSTA_Connect:
		THIS^.sta_CONNECT();
	TCP_CmdState.cSTA_CloseBusy:
		THIS^.sta_CLOSEBusy();
END_CASE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_CLOSE" Id="{47f3292b-7439-43f8-81ff-d5483f06761c}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_CLOSE'}
METHOD sta_CLOSE : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		;
		
	END_IF

// --------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		CASE me.intCmd OF
			TCP_CmdState.cCMD_Connect:
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_ConnectBusy;
		END_CASE
	END_IF

// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_CONNECTBusy" Id="{5e77cbec-e67c-4c84-b6f6-7e55aead2e7b}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_CONNECTBusy'}
METHOD sta_CONNECTBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		// Initialize and connect socket
		fbClientConnection.nMode := 0; 	// not used
		fbClientConnection.sRemoteHost := SIf.Par.strIPAdr;
		fbClientConnection.nRemotePort := SIf.Par.udiPort;
		fbClientConnection.tReconnect := SIF.Par.tTOConnection;
		fbClientConnection.bEnable:= TRUE;
	
		// clear both buffers
		fbSendBuffer.A_Reset();
		fbReceiveBuffer.A_Reset();
		
		// reset all nessecary parameters
		bReceivedAck := 0;
		bReceivedNack := 0;
		intSendState := 0;
	END_IF


// --------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		// Check about FUB error	
		IF (fbClientConnection.bError = TRUE) OR (fbClientConnection.nErrId <> 0) THEN		
			SIf.Cur.bolFubErrorActive:= TRUE;																			
			pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.FubErrorClientConnection, UDINT_TO_DINT(fbClientConnection.nErrId),ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);																			
			me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;	
		END_IF
				
		// Wait for socket handle ID and connection state
		IF (fbClientConnection.hSocket.handle <> 0) AND (fbClientConnection.eState = TC2_TCPIP.E_SocketConnectionState.eSOCKET_CONNECTED) THEN 	
			me.StateMachine.intStateNext := TCP_CmdState.cSTA_Connect;
		END_IF
		
	END_IF

// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="CtrlSIf" Id="{61395e9e-4929-4e3c-b49a-241234675ebf}">
      <Declaration><![CDATA[PROPERTY CtrlSIf : POINTER TO T_Ctrl_SIf_TCP
]]></Declaration>
      <Get Name="Get" Id="{6eca0612-60c3-485a-b8ff-b3d5380d3101}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[CtrlSIf := ADR(SIf);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ReceiveDataSequence" Id="{6223387b-56c6-4f61-b93b-45a4f434992c}">
      <Declaration><![CDATA[{attribute 'object_name' := 'ReceiveDataSequence'}
METHOD PROTECTED ReceiveDataSequence : BOOL
VAR
	bSuccess: BOOL;
	nLoad: UDINT;
	stACK: ST_Header;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// receive data if connected
IF (fbClientConnection.eState = TC2_TCPIP.E_SocketConnectionState.eSOCKET_CONNECTED) THEN
	IF fbReceive.bBusy THEN	
		fbReceive.bExecute := FALSE;
		fbReceive.sSrvNetId := '';
		fbReceive();
		
		IF NOT (fbReceive.bBusy) THEN
			// Receive is finished => see what we got
				
			// First Check about FUB error	
			IF (fbReceive.bError = TRUE) THEN
				IF NOT (SIf.Cur.bolFubErrorActive) THEN
					SIf.Cur.bolFubErrorActive:= TRUE;																				
					pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.FubErrorReceiveData, UDINT_TO_DINT(fbReceive.nErrId),ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);																						
					// Try to close the socket. Needs reconnect for socket after an error!	
					me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;					
				END_IF	
				
			// No error			
			ELSE
				// Check received data
				CASE pReceiveHead^.uinMsgId  OF
					// ACK
					// **************************
					999: 
						bReceivedAck := 1;
					
					// NACK
					// **************************
					998: 
						bReceivedNack := 1;
						
					// Valid message IDs
					// **************************
					2, 3, 4, 5, 6, 7: 
					
						fbReceiveBuffer.A_Add(in := ADR(naReceiveData), iAddSize:= UDINT_TO_INT(SIZEOF(ST_Header) + pReceiveHead^.udiMsgSize), bOk=>bSuccess , nLoad => nLoad );
						IF NOT ( bSuccess ) THEN 
							pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.AddToReceiveBufferFailed,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);
							me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;
							me.StateMachine.intState := BASE_PLC.cSTA_ERROR;
						END_IF
								
					// nothing has been received	
					// **************************
					0: 
					
				ELSE
					// send NACK Command
					IF(SIf.Cfg.bUsingAck) THEN
						stACK.uinMsgId := 998;
						fbSendBuffer.A_Add(in := ADR(stACK), bOk=>bSuccess , nLoad => nLoad, iAddSize:= SIZEOF(stACK.uinMsgId) );
						IF NOT (bSuccess) THEN 
							pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.AddToSendBufferFailed,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);
							me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;
							me.StateMachine.intState := BASE_PLC.cSTA_ERROR;
						END_IF
					END_IF
				
				END_CASE
				// empty the receive data
				pReceiveHead^.uinMsgId := 0;			
			END_IF	
			
		END_IF
		
	// Trigger the next data receive 
	ELSE
		fbReceive(bExecute := FALSE); // Needs new trigger!
		fbReceive.sSrvNetId := '';
		fbReceive.hSocket := fbClientConnection.hSocket;
		fbReceive.cbLen := SIZEOF(naReceiveData);
		fbReceive.pDest := ADR(naReceiveData);
		fbReceive.tTimeout := SIf.Par.tTOReceive;
		fbReceive.bExecute := TRUE;
		fbReceive();
	END_IF
END_IF 
fbReceive();]]></ST>
      </Implementation>
    </Method>
    <Method Name="SendDataSequence" Id="{7678623a-1051-42f6-b7bd-f6b96cc6e55f}">
      <Declaration><![CDATA[{attribute 'object_name' := 'SendDataSequence'}
METHOD PROTECTED SendDataSequence : BOOL
VAR
	intI: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Handle Send Data
CASE intSendState OF
	cSendIdle: 
		IF fbSendBuffer.nCount > 0 AND NOT (fbSend.bBusy) AND (fbClientConnection.eState = TC2_TCPIP.E_SocketConnectionState.eSOCKET_CONNECTED) THEN // there is something in the buffer that needs to be sent
			intSendState := cSendBusy;
			
			// Remove data from buffer. Get next package.
			fbSendBuffer.A_Get(pHeader => pSendHeader);
			pByteSend := pSendHeader;
			stNextHead := pSendHeader^;
			
			IF (stNextHead.uinMsgId = 999) OR (stNextHead.uinMsgId = 998 ) THEN // ack or nack
				iSendSize := SIZEOF(stNextHead.uinMsgId);
			ELSE
				iSendSize := stNextHead.udiMsgSize + SIZEOF(ST_Header);
			END_IF
			
			// send Command
			fbSend(bExecute := FALSE, sSrvNetId := ''); 
			fbSend.sSrvNetId := ''; // SIf.Par.strIPAdr;
			fbSend.hSocket := fbClientConnection.hSocket;
			fbSend.cbLen := iSendSize;
			fbSend.pSrc := pByteSend;
			fbSend.bExecute := TRUE;
			fbSend.tTimeout := SIf.Par.tTOSend;
			fbSend(); 
		ELSE
			fbSend.bExecute := FALSE;
			fbSend.sSrvNetId := '';
			fbSend();
		END_IF	
		// *)
	cSendBusy:
		IF NOT (fbSend.bBusy) THEN // sending done
			
			// First Check about FUB error	
			IF (fbSend.bError = TRUE) THEN
				IF NOT (SIf.Cur.bolFubErrorActive) THEN
					SIf.Cur.bolFubErrorActive:= TRUE;																				
					pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.FubErrorSendData, UDINT_TO_DINT(fbSend.nErrId),ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);																						
					// Try to close the socket. Needs reconnect for socket after an error!	
					me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;					
				END_IF	
			// no error			
			ELSE
				IF (stNextHead.uinMsgId > 997) OR (NOT (SIf.Cfg.bUsingAck)) THEN // the sent was ack or nack
					fbSendBuffer.A_Remove();
					intSendState := cSendIdle;
				ELSE // wair for ack
					intSendState := cSendWait;
					tonSendTime(IN := 0);
					tonSendTime.PT := SIf.Par.iMaxWaitTimeAck;
					tonSendTime(IN := 1);
				END_IF	
			END_IF
			
		END_IF
		
		fbSend.bExecute := FALSE;
		fbSend.sSrvNetId := '';
		fbSend(); // *)
		
	cSendWait:
		fbSend.bExecute := FALSE;
		fbSend.sSrvNetId := '';
		fbSend();
		tonSendTime(IN := 1);
		// Wait for the ACK
		IF bReceivedAck THEN
			fbSendBuffer.A_Remove();
			bReceivedAck := 0;
			iNumSendTry := 0; // reset for next send
			intSendState := cSendIdle;
			
		ELSIF (tonSendTime.Q) OR (bReceivedNack) THEN // timeout
			
			iNumSendTry := iNumSendTry +1;
			IF iNumSendTry >= SIf.Par.iMaxSendTry THEN
				// the signal has not been acknoledged too many times => error
				pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.NoAckReceived,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);
				me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;
			ELSE // retry sending
				fbSend.bExecute := TRUE;
				fbSend(); 
				intSendState := cSendBusy;
			END_IF
			
		END_IF
END_CASE // *)
fbSend();]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Push" Id="{7883c642-dc3f-4d2e-8350-26171b083dbe}">
      <Declaration><![CDATA[{attribute 'object_name' := 'M_Push'}
METHOD M_Push : BOOL
VAR_INPUT
	paPushMsg: POINTER TO ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
END_VAR
VAR
	pstPushMsg: POINTER TO ST_Header;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// send if desired
pstPushMsg := paPushMsg;
	
fbSendBuffer.A_Add(in := paPushMsg, iAddSize := UDINT_TO_INT(pstPushMsg^.udiMsgSize + SIZEOF(ST_HEADER)) , bOk=>M_Push );

IF NOT ( M_Push ) THEN 
	pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.AddToSendBufferFailed,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);
	me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;
END_IF
			
]]></ST>
      </Implementation>
    </Method>
    <Method Name="getCtrlSIf" Id="{93d2be0a-cef5-4a77-8dd0-13d55ddeeb32}">
      <Declaration><![CDATA[{attribute 'object_name' := 'getCtrlSIf'}
METHOD getCtrlSIf : POINTER TO T_Ctrl_SIf_TCP
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getCtrlSIf := ADR(SIf);]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ERROR" Id="{97cd2621-3b4d-46ec-8472-5429b0b9a4c2}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_ERROR'}
METHOD PROTECTED sta_ERROR : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateHistory := me.StateMachine.intStateLastCycle; // -> possibillity to go back to state before error
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		// Clear pending commands
		CIf.Job.intACmd:= Base_PLC.cCMD_DONE;
		CIf.Job.intMCmd:= Base_PLC.cCMD_DONE;
			
		iNumSendTry := 0;
				
		// clear both buffers
		fbSendBuffer.A_Reset();
		fbReceiveBuffer.A_Reset();
		// Reset Tx state
		intSendState:= cSendIdle;
	END_IF
	

// --------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		// State Change Condition
		IF (CIf.Job.bolAlmReset = TRUE) THEN
			SIf.Cur.bolFubErrorActive:= FALSE;
			me.StateMachine.intStateNext := cSTA_ERRORQuit;
		END_IF
	END_IF

// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{9f31c568-7162-437c-8e00-9f15ed125f3e}">
      <Declaration><![CDATA[{attribute 'object_name' := 'FB_init'}
METHOD FB_init : BOOL
VAR_INPUT
	/// if TRUE, the retain variables are initialized (warm start / cold start)
	bInitRetains: BOOL;
	/// if TRUE, the instance afterwards gets moved into the copy code (online change)
	bInCopyCode: BOOL;
END_VAR
VAR
	iIdx: UINT;
	iLength: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// ------------------------------------------------------------------------------------------------------------------------
// to manually change in new FUB implementation!!!
// define valid commands for this controller
iLength := SIZEOF(CIf.Job.aCmdList)/SIZEOF(CIf.Job.aCmdList[0]);
iIdx := 0;

WHILE (iIdx < iLength) AND (CIf.Job.aCmdList[iIdx].intCmd <> cCMD_INITIALIZED) DO
	iIdx := iIdx + 1; 
END_WHILE

IF (iIdx < iLength) THEN
// Add configuration of own comands	
	CIf.Job.aCmdList[iIdx].intCmd := TCP_CmdState.cCMD_Close;
	CIf.Job.aCmdList[iIdx].strName := 'CLOSE'; 
	CIf.Job.aCmdList[iIdx].bolAvailable := TRUE;
    iIdx := iIdx + 1;
		CIf.Job.aCmdList[iIdx].intCmd := TCP_CmdState.cCMD_Connect;
	CIf.Job.aCmdList[iIdx].strName := 'CONNECT'; 
	CIf.Job.aCmdList[iIdx].bolAvailable := TRUE;
    iIdx := iIdx + 1;

ELSE
	// Error if the aCmdList Array is to small
END_IF

// set name of class/typ
CIf.Info.strType := 'FB_TCP';
CIf.Info.strFullName := 'TCP Communication Driver';

// set alarm class
SIf.Alm[E_ALM_TCP.AddToReceiveBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.AddToReceiveBufferFailed].strText := 'addReceiveBufferFailed'; 
SIf.Alm[E_ALM_TCP.AddToSendBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.AddToSendBufferFailed].strText := 'addSendBufferFailed'; 
SIf.Alm[E_ALM_TCP.GetFromReceiveBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.GetFromReceiveBufferFailed].strText := 'getReceiveBufferFailed'; 
SIf.Alm[E_ALM_TCP.GetFromSendBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.GetFromSendBufferFailed].strText := 'getSendBufferFailed'; 
SIf.Alm[E_ALM_TCP.RemoveFromReceiveBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.RemoveFromReceiveBufferFailed].strText := 'RemoveReceiveBufferFailed'; 
SIf.Alm[E_ALM_TCP.RemoveFromSendBufferFailed].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.RemoveFromSendBufferFailed].strText := 'RemoveSendBufferFailed'; 
SIf.Alm[E_ALM_TCP.SocketConnectionLost].eAlmClass := E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.SocketConnectionLost].strText := 'SocketLost'; 
SIf.Alm[E_ALM_TCP.NotSent].eAlmClass := BASE_PLC.E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.NotSent].strText := 'NotSent';
SIf.Alm[E_ALM_TCP.NoAckReceived].eAlmClass := BASE_PLC.E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.NoAckReceived].strText := 'NoAckReceived';
SIf.Alm[E_ALM_TCP.FubErrorClientConnection].eAlmClass := BASE_PLC.E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.FubErrorClientConnection].strText := 'FUB error - client connection. Note add. info number';
SIf.Alm[E_ALM_TCP.FubErrorSendData].eAlmClass := BASE_PLC.E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.FubErrorSendData].strText := 'FUB error - send data. Note add. info number';
SIf.Alm[E_ALM_TCP.FubErrorReceiveData].eAlmClass := BASE_PLC.E_AlmClass.INFO;
SIf.Alm[E_ALM_TCP.FubErrorReceiveData].strText := 'FUB error - receive data. Note add. info number';


// *******************************************************************************
// initial values
// *******************************************************************************

// software configuration
CIf.SwCfg.bolExist := TRUE;
CIf.SwCfg.bolEnable := TRUE;
CIf.SwCfg.bolSimulation :=  FALSE;

// configuration
SIf.Cfg.bolAutoReconnectEnable  := TRUE;


// parameter
SIF.Par.strIPAdr := '192.168.2.182'; 
SIF.Par.udiPort := 7443;
SIF.Par.tTOConnection := T#10S;
SIF.Par.tTOReceive := T#5S;
SIF.Par.tTOSend := T#5S;
SIf.Par.iMaxWaitTimeAck := T#4S; // wait for Ack before declaring it not received
SIf.Par.iMaxSendTry := 5;

SIf.Cfg.bUsingAck := TRUE;

pReceiveData := ADR(naReceiveData);
pReceiveHead := pReceiveData;]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_CLOSEBusy" Id="{a785f369-92e3-4aee-a34d-b351690afae6}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_CLOSEBusy'}
METHOD sta_CLOSEBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		// Reset Tx state
		intSendState:= cSendIdle;
		fbSend.bExecute:= FALSE;
		fbReceive.bExecute:= FALSE;
		iNumSendTry := 0;
		
		// Close socket
		fbClientConnection.bEnable:= FALSE;
		
		IF me.intCmd = TCP_CmdState.cCMD_Close THEN
			// clear both buffers
			fbSendBuffer.A_Reset();
			fbReceiveBuffer.A_Reset();
		END_IF
	END_IF

// --------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		// Check about FUB error	
		IF (fbClientConnection.bError = TRUE) OR (fbClientConnection.nErrId <> 0) THEN
			SIf.Cur.bolFubErrorActive:= TRUE;																					
			pfbAlarmCtrl^.setAlarm(CIf.Info.intCtrlId,E_ALM_TCP.FubErrorClientConnection, UDINT_TO_DINT(fbClientConnection.nErrId),ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);																			
			me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;	
		END_IF
					
		// Wait until socket is closed	
		IF (fbClientConnection.eState = TC2_TCPIP.E_SocketConnectionState.eSOCKET_DISCONNECTED) THEN 	
			IF me.intCmd = TCP_CmdState.cCMD_Close THEN
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_Close;
			ELSE
				me.StateMachine.intStateNext := TCP_CmdState.cSTA_ConnectBusy;
			END_IF

		ELSIF (SIf.Cur.bolFubErrorActive) THEN
			me.StateMachine.intStateNext := BASE_PLC.cSTA_ERROR;	
		END_IF
		
	END_IF
	
// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CycleEndDuty" Id="{c30c6ca9-5d88-4952-9272-3ecf84574e81}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CycleEndDuty'}
METHOD PROTECTED CycleEndDuty : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.CycleEndDuty();

SendDataSequence();]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ERRORQuit" Id="{c4893612-b17e-49b2-a4fe-fe76eec50cbf}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_ERRORQuit'}
METHOD PROTECTED sta_ERRORQuit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
// --------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		;
	END_IF

// -------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		IF (CIf.Job.bolAlmReset = TRUE) THEN
			CIf.Job.eMe_ActivAlmClass := E_AlmClass.NONE; // reset feedback channel for parent
			CIf.Job.bolAlmReset := FALSE;
		END_IF
		
		// State Change Condition
		IF (CIf.Job.bolAlmReset = FALSE) THEN
			// Needs reconnect for socket after an error!	
			me.StateMachine.intStateNext := TCP_CmdState.cSTA_CloseBusy;
		END_IF
	END_IF

// --------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="getCmdState" Id="{ca134720-35a0-4cc7-b397-1701408cae06}">
      <Declaration><![CDATA[{attribute 'object_name' := 'getCmdState'}
METHOD PROTECTED getCmdState : STRING
VAR_INPUT
	intCmdState: INT;
	bolState: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getCmdState := SUPER^.getCmdState(intCmdState:=intCmdState,bolState:=bolState); // get global/base defined cmds/states

IF (getCmdState='') THEN // global not defined get local
	// get string cmd/state string information in runtime	
	IF (bolState=FALSE) THEN
		// States's
		CASE intCmdState OF
			TCP_CmdState.cCMD_Connect: 
				getCmdState := 'cCMD_CONNECT';
			TCP_CmdState.cCMD_Close: 
				getCmdState := 'cCMD_CLOSE';

		END_CASE;
	ELSE
		// States's
		CASE intCmdState OF
			TCP_CmdState.cSTA_Close: 
				getCmdState := 'cSTA_CLOSE';
			TCP_CmdState.cSTA_CloseBusy: 
				getCmdState := 'cSTA_CLOSEBusy';
			TCP_CmdState.cSTA_Connect: 
				getCmdState := 'cSTA_CONNECT';
			TCP_CmdState.cSTA_ConnectBusy: 
				getCmdState := 'cSTA_CONNECTBusy';			
		END_CASE;
	END_IF 
END_IF 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="CtrlInit" Id="{d3032c55-8d3a-4753-8283-b1aeab934c50}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CtrlInit'}
METHOD CtrlInit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// one time first cyclic call
// The socket is closed
SUPER^.CtrlInit();

me.StateMachine.intStateNext := TCP_CmdState.cSTA_Close;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CycleStartDuty" Id="{e5befbef-7a13-4c15-81d6-9234fa61bb5a}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CycleStartDuty'}
METHOD PROTECTED CycleStartDuty : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.CycleStartDuty();

ReceiveDataSequence();]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Pop" Id="{ec84a9f0-340d-48b3-8bd3-8828de31544b}">
      <Declaration><![CDATA[{attribute 'object_name' := 'M_Pop'}
METHOD M_Pop : BOOL
VAR_OUTPUT
	naPopMsg: ARRAY[0..MAX_DATASET_SIZE] OF BYTE;
	bPop: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF fbReceiveBuffer.nCount > 0 THEN // there is something in the receive buffer
	fbReceiveBuffer.A_Remove( pHeader => pByteReceive );
	naPopMsg := pByteReceive^;
	M_Pop := 1;
	bPop := 1;
ELSE
	M_Pop := 0;
	bPop := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>